reset

#Lancer gnuplot.
# Taper :
# load "animation_2pv_2.gplot"
# (en étant dans le bon répertoire, avec le bon format de fichier, à adapter en fonction de vos besoins/formats, ...)

# Paramètres à adapter
# la distance entre les points
D = 1.0
DT = 0.01
NIT = 5000
Debut = 500
# les positions des points
array P1_0[2] = [+D/2.0,0.0]
array P2_0[2] = [-D/2.0,0.0]

# les circulations (choisir 1 parmi)
array omega[2] = [1.0,+1.0]
# array omega[2] = [1.0,1.0]
# array omega[2] = [1.0,0.5]
# array omega[2] = [1.0,+3.0]
# array omega[2] = [1.0,-3.0]
# etc.
omega_sum = omega[1]+omega[2]
gamma = omega[1] / omega_sum

# Calculs pour la solution analytique à 2 vortex générale ($\omega_1 \ne \omega_2$)

# le centre de vorticité
array C_vort[2]
C_vort[1] = (omega[1]*P1_0[1] + omega[2]*P2_0[1])/omega_sum
C_vort[2] = (omega[1]*P1_0[2] + omega[2]*P2_0[2])/omega_sum

print "Vortex center: ", C_vort

theta = omega_sum/(2 * pi * D**2)

P1_x(t) = C_vort[1] + (1.0 - gamma)*D*cos(theta*t)
P1_y(t) = C_vort[2] + (1.0 - gamma)*D*sin(theta*t)

P2_x(t) = C_vort[1] - gamma*D*cos(theta*t)
P2_y(t) = C_vort[2] - gamma*D*sin(theta*t)

S = 0.6 * D # Scale
set xrange [-S:+S]
set yrange [-S:+S]
set size square

set object 1 circle at C_vort[1],C_vort[2] size gamma*D*omega[2] fs empty border lc rgb "navy" lw 2
set object 2 circle at C_vort[1],C_vort[2] size gamma*D*omega[1] fs empty border lc rgb "navy" lw 2

# Les données sont stockées dans les fichiers results/points_vortex_euler.txt_0, results/points_vortex_euler.txt_1, results/points_vortex_euler.txt_2, etc.
# sous la forme (par fichier) :
# x_1 y_1 w_1
# x_2 y_2 w_2
# x_3 y_3 w_3
# etc.

# Juste les points numériques
# do for [i=Debut:NIT] { plot "results/points_vortex_euler.txt_".i u 1:2 w p ; pause 0.01; }
# Les points numériques + analytique
do for [i=Debut:NIT] {
    plot "results/points_vortex.txt_".i u 1:2 w p pt 7 ps 1.5 lc rgb "red" t "Numérique"
    set parametric
    replot P1_x(i*DT), P1_y(i*DT) w p pt 6 ps 1.5 lc rgb "green" t "Analytique : Vortex 1"
    replot P2_x(i*DT), P2_y(i*DT) w p pt 6 ps 1.5 lc rgb "purple" t "Analytique : Vortex 2"
    unset parametric
    pause 0.01;
}